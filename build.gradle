apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'gradle-mongo-plugin'
group = 'com.sourcemuse.gradle.plugin'
version = '0.2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.42'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Gradle Mongo Plugin'
                packaging jar
                description 'Gradle plugin for managing a local instance of MongoDb'
                url 'https://github.com/sourcemuse/GradleMongoPlugin'

                scm {
                    url 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    connection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    developerConnection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Mashooq Badar'
                    }
                    developer {
                        name 'Robert Taylor'
                    }
                }
            }
        }
    }
}
