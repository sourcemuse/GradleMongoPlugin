apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'gradle-mongo-plugin'
group = 'com.sourcemuse.gradle.plugin'
version = '0.9.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.0'

    testCompile files('libs/gradle-test-kit.jar')
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.mongodb:mongo-java-driver:3.1.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
            }

            pom.project {
                name 'Gradle Mongo Plugin'
                packaging jar
                description 'Gradle plugin for managing a local instance of MongoDb'
                url 'https://github.com/sourcemuse/GradleMongoPlugin'

                scm {
                    url 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    connection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    developerConnection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Mashooq Badar'
                    }
                    developer {
                        name 'Robert Taylor'
                    }
                }
            }
        }
    }
}

String getSonatypeUsername() {
    properties['sonatypeUsername'] ?: 'username'
}

String getSonatypePassword() {
    properties['sonatypePassword'] ?: 'password'
}
